rem ' replicates writes to the Barista Add-on GL Account Master back to the Add-On 6.1 Account Master
rem ' Barista glm-01  --  6.1 GLM-01

seterr handle_err

rem ' set opts
SETOPTS $0800C02000002040$

rem ' if this is removed, be sure to remove the write to trace in the error handler
trace = unt
open(trace,mode="O_TRUNC")"/mnt/data/basisaon/aon/trig_trace/trace-glm-01.log"
settrace(trace)

rem ' set global to the old GL
trash$=stbl("ADDON_OLDGL","/mnt/data/basisaon/aon/ADATA/")

rem ' Barista Add-On glm-01 template
inTemp$="FIRM_ID:C(2),GL_ACCOUNT:C(10*=10),GL_ACCT_DESC:C(35),GL_ACCT_TYPE:C(1),DETAIL_FLAG:C(1),RESERVED_STR:C(27*=10)"
dim inRec$:inTemp$

rem ' 6.1 GLM-01 template
outTemp$ = "FIRM_ID:C(2),GL_ACCOUNT:C(10*=10),GL_ACCT_DESC:C(35),GL_ACCT_TYPE:C(1),DETAIL_FLAG:C(1),RESERVED_STR:C(3*=10)"
dim outRec$:outTemp$

rem '----declares
Declare BBjTriggerData trigger!

rem '----get the trigger data
trigger! = BBJAPI().getFileSystem().getTriggerData()

rem '----get the write buffer into the template
inRec$ = trigger!.getWriteBuffer()
inRec$ = field(inRec$)

rem '----assign the data
outRec.FIRM_ID$ = inRec.FIRM_ID$
outRec.GL_ACCOUNT$ = inRec.GL_ACCOUNT$
outRec.GL_ACCT_DESC$ = inRec.GL_ACCT_DESC$
outRec.GL_ACCT_TYPE$ = inRec.GL_ACCT_TYPE$
outRec.DETAIL_FLAG$ = inRec.DETAIL_FLAG$
outRec.RESERVED_STR$ = "   "

rem '----open the target output file
outRec = unt
write(trace)stbl("ADDON_OLDGL")
open(outRec)stbl("ADDON_OLDGL")+"GLM-01"


rem '----write the data
write record(outRec,key=outRec.FIRM_ID$ + outRec.GL_ACCOUNT$)outRec$
close(outRec)

end

handle_err:
    write (trace)"error: " + str(err) + " line " + str(tcb(5))
    throw "An error " + str(err) + " occurred on line " + str(tcb(5)) + " of the trigger glm-01.trigger.aw.src.", 275
